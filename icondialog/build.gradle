apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'icon-dialog'
    bintrayName = 'icon-dialog'

    publishedGroupId = 'com.maltaisn'
    libraryName = 'icon-dialog'
    artifact = 'icondialog'
    packagingType = 'jar'

    libraryDescription = 'Icon dialog for Android'

    siteUrl = 'https://github.com/maltaisn/icondialoglib'
    gitUrl = 'https://github.com/maltaisn/icondialoglib.git'

    libraryVersionCode = 7
    libraryVersion = '2.0.1'

    developerId = 'maltaisn'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27

        versionCode libraryVersionCode
        versionName libraryVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // A new entry to this array must be added for every new search translation!
        // The array is used by the dialog to know which languages are available for search
        def lang = ["en", "fr", "pt"]
        buildConfigField "String[]", "ICD_LANG", "new String[]{\"" + lang.join("\",\"") + "\"}"
        resConfigs lang
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
}

apply from: 'maven-install.gradle'
apply from: 'bintray.gradle'